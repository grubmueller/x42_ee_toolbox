@ R||R: Parallel resistance of two resistors
@
@ PParallel resistance of two resistors (also works with complex impedances):
@ R = R1*R2/(R1+R2)
@ Program emulates the solver behaviour.
@ Input/output paramenter:
@ [R1]: Resistor 1
@ [R2]: Resistor 2
@ [R]: Parallel resistance
@
@ Michael Grubm√ºller, michael.grubmueller@gmail.com
@ 2021-04-19

00 { 136-Byte Prgm }
01 LBL "R||R"
02 MVAR "R1"
03 MVAR "R2"
04 MVAR "R"
05 VARMENU "R||R"

06 CF 21 @ Do not stop on VIEW

07 LBL a
08 STOP
09 ASTO ST X @ Write variable name of unknown to X stack
10 STO "UKNOWN"

11 "R"
12 ASTO ST Y
13 X=Y?
14 GTO A

15 "R1"
16 ASTO ST Y
17 X=Y?
18 GTO B

19 "R2"
20 ASTO ST Y
21 X=Y?
22 GTO C

@ Show and save result
23 LBL b
24 STO IND "UKNOWN"
25 VIEW IND "UKNOWN"
26 GTO a

@ Parallel impedance
27 LBL A
28 RCL "R1"
29 1/X
30 RCL "R2"
31 1/X
32 +
33 1/X
34 GTO b

@ Impedance 1
35 LBL B
36 RCL "R"
37 1/X
38 RCL "R2"
39 1/X
40 -
41 1/X
42 GTO b

@ Impedance 2
43 LBL C
44 RCL "R"
45 1/X
46 RCL "R1"
47 1/X
48 -
49 1/X
50 GTO b

51 END
