@ LC-R1: LC resonant circuit design
@
@ Resonant frequency or component calculation of LC resonant cirucit:
@ f0 = 1/(2*pi*sqrt(L*C))
@ Program emulates the solver behaviour.
@ Input/output parameters:
@ [L]: Inductance in H
@ [C]: Capacitance in F
@ [f0]: Resonant frequency in Hz
@
@ Michael Grubmüller, michael.grubmueller@gmail.com
@ 2021-04-13

00 { 143-Byte Prgm }
01 LBL "LC-R1"
02 MVAR "L"
03 MVAR "C"
04 MVAR "f0"
05 VARMENU "LC-R1"

06 CF 21 @ Do not stop on VIEW

07 LBL a
08 STOP
09 ASTO ST X @ Write variable name of unknown to X stack
10 STO "UKNOWN"

11 "L"
12 ASTO ST Y
13 X=Y?
14 GTO A

15 "C"
16 ASTO ST Y
17 X=Y?
18 GTO B

19 "f0"
20 ASTO ST Y
21 X=Y?
22 GTO C

@ Show and save result
23 LBL b
24 STO IND "UKNOWN"
25 VIEW IND "UKNOWN"
26 GTO a

@ Inductor L
27 LBL A
28 RCL "f0"
29 2
30 ×
31 PI
32 ×
33 X↑2
34 RCL× "C"
35 1/X
36 GTO b

@ Capacitor C
37 LBL B
38 RCL "f0"
39 2
40 ×
41 PI
42 ×
43 X↑2
44 RCL× "L"
45 1/X
46 GTO b

@ Resonant frequency f0
47 LBL C
48 RCL "L"
49 RCL× "C"
50 SQRT
51 2
52 ×
53 PI
54 ×
55 1/X
56 GTO b

57 END
