@ LC-R2: LC resonant circuit design (advanced version)
@
@ Step through menus with [UP] and [DOWN] arrows, or exit with [EXIT].
@ For variable input INPUT ist used, so confirm with [R/S].
@ Menu page 1: Toggle [SER]ies or [PAR]allel resonant circuit;
@ calculate [→f0] and [→Q] out of [L], [C] and [R];
@ Menu page 2: Toggle [SER]ies or [PAR]allel resonant circuit;
@ Calculate [→L] and [→C] for desired [f0] and [Q]
@ Menu page 3: Calculate [f0], [Q] or [B] out of the other 2
@ Requires "LIB" programs of repository.
@
@ Michael Grubmüller, michael.grubmueller@gmail.com
@ 2021-04-12

00 { 523-Byte Prgm }
01 LBL "LC-R2"

02 XEQ "STOST"
03 XEQ "STOD"

@ Menu page 1
04 LBL a
05 CLMENU
06 "L"
07 KEY 1 XEQ 01
08 "C"
09 KEY 2 XEQ 02
10 "R"
11 KEY 3 XEQ 03
12 FS? 85
13 "SER"
14 FC? 85
15 "PAR"
16 KEY 4 XEQ 07
17 "→f0"
18 KEY 5 XEQ A
19 "→Q"
20 KEY 6 XEQ B
21 KEY 7 GTO c @ [UP]
22 KEY 8 GTO b @ [DOWN]
23 KEY 9 GTO e @ [EXIT]
24 MENU
25 STOP
26 GTO a

@ Menu page 2
27 LBL b
28 CLMENU
29 "f0"
30 KEY 1 XEQ 04
31 "Q"
32 KEY 2 XEQ 05
33 "R"
34 KEY 3 XEQ 03
35 FS? 85
36 "SER"
37 FC? 85
38 "PAR"
39 KEY 4 XEQ 07
40 "→L"
41 KEY 5 XEQ C
42 "→C"
43 KEY 6 XEQ D
44 KEY 7 GTO a @ [UP]
45 KEY 8 GTO c @ [DOWN]
46 KEY 9 GTO e @ [EXIT]
47 MENU
48 STOP
49 GTO b

@ Menu page 3
50 LBL c
51 CLMENU
52 "f0"
53 KEY 1 XEQ 04
54 "Q"
55 KEY 2 XEQ 05
56 "B"
57 KEY 3 XEQ 06
58 "→f0"
59 KEY 4 XEQ E
60 "→Q"
61 KEY 5 XEQ F
62 "→B"
63 KEY 6 XEQ G
64 KEY 7 GTO b @ [UP]
65 KEY 8 GTO a @ [DOWN]
66 KEY 9 GTO e @ [EXIT]
67 MENU
68 STOP
69 GTO c

@ Input L
70 LBL 01
71 INPUT "L"
72 RTN

@ Input C
73 LBL 02
74 INPUT "C"
75 RTN

@ Input R
76 LBL 03
77 INPUT "R"
78 RTN

@ Input f0
79 LBL 04
80 INPUT "f0"
81 RTN

@ Input Q
82 LBL 05
83 INPUT "Q"
84 RTN

@ Input B
85 LBL 06
86 INPUT "B"
87 RTN

@ Invert flag for series/parallel circuit configuration
88 LBL 07
89 FS?C 85
90 RTN
91 SF 85
92 RTN

@ ------------------------------------------------------------------------------
@ Calculate resontant frequency f0
93 LBL A
94 RCL "L"
95 RCL "C"
96 ×
97 SQRT
98 2
99 PI
100 ×
101 ×
102 1/X
103 STO "f0"
104 VIEW "f0"
105 RTN

@ ------------------------------------------------------------------------------
@ Calculate quality factor Q
106 LBL B
107 FC? 85
108 GTO 21

@ Series resonant cirucit
109 RCL "R"
110 1/X
111 RCL "L"
112 RCL "C"
113 ÷
114 SQRT
115 ×
116 GTO 22

@ Parallel resonant circuit
117 LBL 21
118 RCL "R"
119 RCL "C"
120 RCL "L"
121 ÷
122 SQRT
123 ×

@ Store and view
124 LBL 22
125 STO "Q"
126 VIEW "Q"
127 RTN

@ ------------------------------------------------------------------------------
@ Calculate inductance L
128 LBL C

129 FC? 85
130 GTO 31

@ Series resonant cirucit
131 RCL "Q"
132 RCL "R"
133 ×
134 2
135 PI
136 RCL "f0"
137 ×
138 ×
139 ÷
140 GTO 32

@ Parallel resonant circuit
141 LBL 31
142 RCL "R"
143 2
144 PI
145 RCL "f0"
146 ×
147 ×
148 RCL "Q"
149 ×
150 ÷

@ Store and view
151 LBL 32
152 STO "L"
153 VIEW "L"
154 RTN

@ ------------------------------------------------------------------------------
@ Calculate capacitance C
155 LBL D
156 FC? 85
157 GTO 41

@ Series resonant cirucit
158 RCL "Q"
159 RCL "R"
160 ×
161 2
162 PI
163 RCL "f0"
164 ×
165 ×
166 ×
167 1/X
168 GTO 42

@ Parallel resonant circuit
169 LBL 41
170 RCL "Q"
171 2
172 PI
173 RCL "f0"
174 ×
175 ×
176 RCL "R"
177 ×
178 ÷

@ Store and view
179 LBL 42
180 STO "C"
181 VIEW "C"
182 RTN

@ ------------------------------------------------------------------------------
@ Calculate resonant frequency f0
183 LBL E
184 RCL "Q"
185 RCL "B"
186 ×
187 STO "f0"
188 VIEW "f0"
189 RTN

@ ------------------------------------------------------------------------------
@ Calculate quality factor Q
190 LBL F
191 RCL "f0"
192 RCL "B"
193 ÷
194 STO "Q"
195 VIEW "Q"
196 RTN

@ ------------------------------------------------------------------------------
@ Calculate bandwidth B
197 LBL G
198 RCL "f0"
199 RCL "Q"
200 ÷
201 STO "B"
202 VIEW "B"
203 RTN

@ ------------------------------------------------------------------------------
@ End of program
204 LBL e
205 EXITALL
206 XEQ "RCLD"
207 XEQ "RCLST"
208 XEQ "CLTMP"
209 END
