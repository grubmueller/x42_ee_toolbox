@ VREG: Voltage regulator calculation
@
@ Output voltage adjustment of linear regulators (like the LM317):
@ Vout = Vref*(1+R2/R1)+Iadj*R2
@ Program emulates the solver behaviour.
@ Input/output paramenter:
@ [R1]: Top resistance in Ohm
@ [R2]: Bottom resistance in Ohm
@ [Vref]: Reference voltage in V
@ [Iadj]: Adjust pin current in A
@ [Vout]: Output voltage in V
@
@ Michael Grubmüller, michael.grubmueller@gmail.com
@ 2021-04-13

00 { 302-Byte Prgm }
01 LBL "VREG"
02 MVAR "Vref"
03 MVAR "R1"
04 MVAR "R2"
05 MVAR "Iadj"
06 MVAR "Vout"
07 VARMENU "VREG"

08 CF 21 @ Do not stop on VIEW

09 LBL a
10 STOP
11 ASTO ST X @ Write variable name of unknown to X stack
12 STO "UKNOWN"

13 "Vout"
14 ASTO ST Y
15 X=Y?
16 GTO A

17 "Vref"
18 ASTO ST Y
19 X=Y?
20 GTO B

21 "R1"
22 ASTO ST Y
23 X=Y?
24 GTO C

25 "R2"
26 ASTO ST Y
27 X=Y?
28 GTO D

29 "Iadj"
30 ASTO ST Y
31 X=Y?
32 GTO E

@ Show and save result
33 LBL b
34 STO IND "UKNOWN"
35 VIEW IND "UKNOWN"
36 GTO a

@ Output voltage Vout
37 LBL A
38 RCL "R2"
39 RCL÷ "R1"
40 1
41 +
42 RCL× "Vref"
43 RCL "Iadj"
44 RCL× "R2"
45 +
46 GTO b

@ Reference voltage Vref
47 LBL B
48 RCL "Vout"
49 RCL "Iadj"
50 RCL× "R2"
51 -
52 RCL "R2"
53 RCL÷ "R1"
54 1
55 +
56 ÷
57 GTO b

@ Resistor R1
58 LBL C
59 RCL "Vout"
60 RCL- "Vref"
61 RCL "Iadj"
62 RCL× "R2"
63 -
64 RCL "Vref"
65 RCL× "R2"
66 X<>Y
67 ÷
68 GTO b

@ Resistor R2
69 LBL D
70 RCL "Vout"
71 RCL- "Vref"
72 RCL "Vref"
73 RCL÷ "R1"
74 RCL+ "Iadj"
75 ÷
76 GTO b

@ Adjust current Iadj
77 LBL E
78 RCL "R2"
79 RCL÷ "R1"
80 1
81 +
82 RCL× "Vref"
83 RCL "Vout"
84 X<>Y
85 -
86 RCL÷ "R2"
87 GTO b

88 END
