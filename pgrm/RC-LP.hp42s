@ RC-LP: Passive RC low pass filter design
@
@ Cut-off frequency or component value calculation of passive RC filter:
@ fc = 1/(2*pi*R*C)
@ Program emulates the solver behaviour.
@ Input/output paramenter:
@ [R]: Resistance in Ohm
@ [C]: Capcitance in F
@ [fc]: Cut-off frequency in Hz
@
@ Michael Grubmüller, michael.grubmueller@gmail.com
@ 2021-04-13

00 { 137-Byte Prgm }
01 LBL "RC-LP"
02 MVAR "R"
03 MVAR "C"
04 MVAR "fc"
05 VARMENU "RC-LP"

06 CF 21 @ Do not stop on VIEW

07 LBL a
08 STOP
09 ASTO ST X @ Write variable name of unknown to X stack
10 STO "UKNOWN"

11 "fc"
12 ASTO ST Y
13 X=Y?
14 GTO A

15 "R"
16 ASTO ST Y
17 X=Y?
18 GTO B

19 "C"
20 ASTO ST Y
21 X=Y?
22 GTO C

@ Show and save result
23 LBL b
24 STO IND "UKNOWN"
25 VIEW IND "UKNOWN"
26 GTO a

@ Cut-off frequency fc
27 LBL A
28 2
29 PI
30 ×
31 RCL× "R"
32 RCL× "C"
33 1/X
34 GTO b

@ Resistor R
35 LBL B
36 2
37 PI
38 ×
39 RCL× "fc"
40 RCL× "C"
41 1/X
42 GTO b

@ Capacitor C
43 LBL C
44 2
45 PI
46 ×
47 RCL× "fc"
48 RCL× "R"
49 1/X
50 GTO b

51 END
