@ dB: Various dB conversions
@
@ Step through menus with [UP] and [DOWN] arrows, or exit with [EXIT].
@ Menu page 1: Calculation of 20*log... gain
@ [G→dB], [dB→G]
@ Menu page 2: Conversion between linear power and dB, 10*log... 
@ [W→dB], [W→dBm], [W→dBµ], [dB→W], [dBm→W], [dBµ→W]
@ Menu page 3: Conversion between voltage and dBm
@ [50] change the specific impedance, [V→dBm], [dBm→V]
@ The program is meant to be used during a calculation. It replaces the X stack
@ value by the result and restores the remaining stack on program end.
@ Requires "LIB" programs of repository.
@
@ Michael Grubmüller, michael.grubmueller@gmail.com
@ 2021-04-12

00 { 411-Byte Prgm }
01 LBL "dB"

02 XEQ "STOST"
03 XEQ "STOD"
04 50 @ Initialize Z0 with 50 Ohm
05 STO "Z0"

@ Menu page 1
06 LBL a
07 CLMENU
08 "G→dB"
09 KEY 1 XEQ 01
10 "dB→G"
11 KEY 2 XEQ 02
12 KEY 7 GTO c @ [UP]
13 KEY 8 GTO b @ [DOWN]
14 KEY 9 GTO e @ [EXIT]
15 MENU
16 CLST
17 RCL "ST_X"
18 STOP
19 GTO a

@ Menu page 2
20 LBL b
21 CLMENU
22 "W→dB"
23 KEY 1 XEQ 07
24 "W→dBm"
25 KEY 2 XEQ 08
26 "W→dBµ"
27 KEY 3 XEQ 09
28 "dB→W"
29 KEY 4 XEQ 10
30 "dBm→W"
31 KEY 5 XEQ 11
32 "dBµ→W"
33 KEY 6 XEQ 12
34 KEY 7 GTO a @ [UP]
35 KEY 8 GTO c @ [DOWN]
36 KEY 9 GTO e @ [EXIT]
37 MENU
38 CLST
39 RCL "ST_X"
40 STOP
41 GTO b

@ Menu page 3
42 LBL c
43 CLMENU
44 RCL "Z0"
45 CLA
46 AIP
47 KEY 1 XEQ 13
48 "V→dBm"
49 KEY 2 XEQ 14
50 "dBm→V"
51 KEY 3 XEQ 15
52 KEY 7 GTO b @ [UP]
53 KEY 8 GTO a @ [DOWN]
54 KEY 9 GTO e @ [EXIT]
55 MENU
56 CLST
57 RCL "ST_X"
58 STOP
59 GTO c

@ G→dB
60 LBL 01
61 LOG
62 20
63 ×
64 STO "ST_X"
65 RTN

@ dB→G
66 LBL 02
67 20
68 ÷
69 10↑X
70 STO "ST_X"
71 RTN

@ W→dB
72 LBL 07
73 LOG
74 10
75 ×
76 STO "ST_X"
77 RTN

@ W→dBm
78 LBL 08
79 1ᴇ-3
80 ÷
81 LOG
82 10
83 ×
84 STO "ST_X"
85 RTN

@ W→dBµ
86 LBL 09
87 1ᴇ-6
88 ÷
89 LOG
90 10
91 ×
92 STO "ST_X"
93 RTN

@ dB→W
94 LBL 10
95 10
96 ÷
97 10↑X
98 STO "ST_X"
99 RTN

@ dBm→W
100 LBL 11
101 10
102 ÷
103 10↑X
104 1ᴇ-3
105 ×
106 STO "ST_X"
107 RTN

@ dBµ→W
108 LBL 12
109 10
110 ÷
111 10↑X
112 1ᴇ-6
113 ×
114 STO "ST_X"
115 RTN

@ Change Z0
116 LBL 13
117 INPUT "Z0"
118 RTN

@ V→dBm
119 LBL 14
120 X↑2
121 RCL÷ "Z0"
122 XEQ 08
123 STO "ST_X"
124 RTN

@ dBm→V
125 LBL 15
126 XEQ 11
127 RCL× "Z0"
128 SQRT
129 STO "ST_X"
130 RTN

@ End of program
131 LBL e
132 EXITALL
133 XEQ "RCLD"
134 XEQ "RCLST"
135 XEQ "CLTMP"
136 END
