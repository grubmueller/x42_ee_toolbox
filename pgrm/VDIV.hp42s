@ VDIV: Voltage divider
@
@ Simple voltage divider (also works with complex values):
@ Vout = Vin*R2/(R1+R2)
@ Program emulates the solver behaviour.
@ Input/output paramenter:
@ [Vin]: Input voltage in V
@ [R1]: Top resistor in Ohm
@ [R2]: Bottom (output) resistor in Ohm
@ [Vout]: Output voltage in V
@
@ Michael Grubmüller, michael.grubmueller@gmail.com
@ 2021-04-13

00 { 199-Byte Prgm }
01 LBL "VDIV"
02 MVAR "Vin"
03 MVAR "R1"
04 MVAR "R2"
05 MVAR "Vout"
06 VARMENU "VDIV"

07 CF 21 @ Do not stop on VIEW

08 LBL a
09 STOP
10 ASTO ST X @ Write variable name of unknown to X stack
11 STO "UKNOWN"

12 "Vout"
13 ASTO ST Y
14 X=Y?
15 GTO A

16 "Vin"
17 ASTO ST Y
18 X=Y?
19 GTO B

20 "R1"
21 ASTO ST Y
22 X=Y?
23 GTO C

24 "R2"
25 ASTO ST Y
26 X=Y?
27 GTO D

@ Show and save result
28 LBL b
29 STO IND "UKNOWN"
30 VIEW IND "UKNOWN"
31 GTO a

@ Output voltage Vout
32 LBL A
33 RCL "R2"
34 ENTER
35 RCL+ "R1"
36 ÷
37 RCL× "Vin"
38 GTO b

@ Input voltage Vin
39 LBL B
40 RCL "R2"
41 ENTER
42 RCL+ "R1"
43 ÷
44 1/X
45 RCL× "Vout"
46 GTO b

@ Top resistor R1
47 LBL C
48 RCL "Vin"
49 RCL "Vout"
50 ÷
51 1
52 -
53 RCL× "R2"
54 GTO b

@ Bottom resistor R2
55 LBL D
56 RCL "Vin"
57 RCL "Vout"
58 ÷
59 1
60 -
61 1/X
62 RCL× "R1"
63 GTO b

64 END
