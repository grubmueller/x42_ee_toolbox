@ FPBW: Full power bandwidth of op-amp
@
@ Full power bandwidth of an operational amplifier:
@ FPBW = SR/(2*pi*Vp)
@ Program emulates the solver behaviour.
@ Input/output paramenter:
@ [SR]: Amplifier slew rate in V/s (or V/us)
@ [Vp]: Amplifier output voltage (peak) in V
@ [FPBW]: Full power bandwidth in Hz (or MHz)
@
@ Michael Grubmüller, michael.grubmueller@gmail.com
@ 2021-04-13

00 { 153-Byte Prgm }
01 LBL "FPBW"
02 MVAR "SR"
03 MVAR "Vp"
04 MVAR "FPBW"
05 VARMENU "FPBW"

06 CF 21 @ Do not stop on VIEW

07 LBL a
08 STOP
09 ASTO ST X @ Write variable name of unknown to X stack
10 STO "UKNOWN"

11 "FPBW"
12 ASTO ST Y
13 X=Y?
14 GTO A

15 "SR"
16 ASTO ST Y
17 X=Y?
18 GTO B

19 "Vp"
20 ASTO ST Y
21 X=Y?
22 GTO C

@ Show and save result
23 LBL b
24 STO IND "UKNOWN"
25 VIEW IND "UKNOWN"
26 GTO a

@ Full power bandwidth FPBW
27 LBL A
28 2
29 PI
30 ×
31 RCL× "Vp"
32 RCL "SR"
33 X<>Y
34 ÷
35 GTO b

@ Slew rate SR
36 LBL B
37 2
38 PI
39 ×
40 RCL× "Vp"
41 RCL× "FPBW"
42 GTO b

@ Output voltge Vp
43 LBL C
44 RCL "FPBW"
45 RCL "SR"
46 ÷
47 2
48 ×
49 PI
50 ×
51 1/X
52 GTO b

53 END
