@ LP1: Transfer function of a first order low-pass filter
@
@ Complex transfer function of first order low-pass filter:
@ G = 1/(1+j*(f/fc))
@ Program emulates the solver behaviour.
@ Input/output parameter:
@ [f]: Frequency in Hz
@ [fc]: Cut-off frequency in Hz
@ [G]: Complex valued gain; if G is used as an input program works as follows:
@ - G input is real: f or fc are solved for particuar magnitude of G
@ - G input is complex: f or fc are solved for particuar argument of G
@ A further run for G shows the resulting complex value for gain G.
@
@ Michael Grubmüller, michael.grubmueller@gmail.com
@ 2021-04-13

00 { 183-Byte Prgm }
01 LBL "LP1"
02 MVAR "f"
03 MVAR "fc"
04 MVAR "G"
05 VARMENU "LP1"

06 CF 21 @ Do not stop on VIEW

07 LBL a
08 POLAR
09 STOP
10 ASTO ST X @ Write variable name of unknown to X stack
11 STO "UKNOWN"

12 "G"
13 ASTO ST Y
14 X=Y?
15 GTO A

16 "f"
17 ASTO ST Y
18 X=Y?
19 GTO B

20 "fc"
21 ASTO ST Y
22 X=Y?
23 GTO C

@ Show and save result
24 LBL b
25 POLAR
26 STO IND "UKNOWN"
27 VIEW IND "UKNOWN"
28 GTO a

@ ------------------------------------------------------------------------------
@ Solve for gain G
29 LBL A
30 RECT
31 1
32 RCL "f"
33 RCL "fc"
34 ÷
35 COMPLEX
36 1/X
37 GTO b

@ ------------------------------------------------------------------------------
@ Solve for frequency f
38 LBL B
39 RCL "G"
40 CPX?
41 GTO 20

@ Calculate for particular gain
@ mag = 1/SQRT(1+(f/fc)^2)
@ -> f = fc*SQRT(1/mag^2-1)
42 X↑2
43 1/X
44 1
45 -
46 SQRT
47 RCL× "fc"
48 GTO b

@ Calculate for particular phase
@ arg = -ATAN(f/fc)
@ -> f = fc*TAN(-arg)
49 LBL 20
50 POLAR
51 COMPLEX @ puts arg in x stack and abs in y stack
52 +/-
53 TAN
54 RCL× "fc"
55 GTO b

@ ------------------------------------------------------------------------------
@ Solve for cut-off frequency fc
56 LBL C
57 RCL "G"
58 CPX?
59 GTO 30

@ Calculate for particular gain
@ mag = 1/SQRT(1+(f/fc)^2)
@ -> fc = f/SQRT(1/mag^2-1)
60 X↑2
61 1/X
62 1
63 -
64 SQRT
65 1/X
66 RCL× "f"
67 GTO b

@ Calculate for particular phase
@ arg = -ATAN(f/fc)
@ -> fc = f/TAN(-arg)
68 LBL 30
69 POLAR
70 COMPLEX @ puts arg in x stack and abs in y stack
71 +/-
72 TAN
73 1/X
74 RCL× "f"
75 GTO b

76 END
