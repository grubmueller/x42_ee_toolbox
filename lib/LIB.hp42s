@ LIB: Library to save and recall calculator stack and display
@
@ The "STOD" and "RCLD" programs store and recall the display settigns to/from
@ "D_CUSTOM", "D_FMT", "D_DGT" and "D_POLAR".  The temporary variables are not
@ deleted after execution of "RCLD".
@
@ The "STOST" and "RCLST" programs store and recall the stack registers X, Y, Z,
@ T and L to/from variables "ST_X", "ST_Y", "ST_Z", "ST_T" and "ST_L". The stack
@ stays unchanged after "STOST". The temporary variables are not deleted after
@ execution of "RCLST".
@
@ CLTMP clears the temporary variables of the upper functions.
@
@ Michael Grubmüller, michael.grubmueller@gmail.com
@ 2021-04-12

@ STOD: Store display settings -------------------------------------------------
00 { 125-Byte Prgm }
01 LBL "STOD"

@ Custom menu on
02 0
03 FS? 27
04 1
05 STO "D_CUST"

@ Format
@ 0: SCI
@ 1: ENG
@ 2: FIX
@ 3: ALL
06 0
07 STO "D_FMT"
08 CLX
09 FS? 40
10 2
11 STO+ "D_FMT"
12 CLX
13 FS? 41
14 1
15 STO+ "D_FMT"

@ Number of digits
16 0
17 STO "D_DGT"
18 CLX
19 FS? 36
20 8
21 STO+ "D_DGT"
22 CLX
23 FS? 37
24 4
25 STO+ "D_DGT"
26 CLX
27 FS? 38
28 2
29 STO+ "D_DGT"
30 CLX
31 FS? 39
32 1
33 STO+ "D_DGT"

@ Complex format
34 0
35 FS? 73
36 1
37 STO "D_POL"

38 END

@ RCLD: Recall display settings ------------------------------------------------
00 { 74-Byte Prgm }
01 LBL "RCLD"

@ Custom menu
02 RCL "D_CUST"
03 X≠0?
04 SF 27

@ Display
05 RCL "D_FMT"
06 0
07 X=Y?
08 SCI IND "D_DGT"
09 CLX
10 1
11 X=Y?
12 ENG IND "D_DGT"
13 CLX
14 2
15 X=Y?
16 FIX IND "D_DGT"
17 CLX
18 3
19 X=Y?
20 ALL

@ Complex format
21 RCL "D_POL"
22 X≠0?
23 POLAR

24 END

@ STOST: Store stack -----------------------------------------------------------
00 { 58-Byte Prgm }
01 LBL "STOST"

02 STO "ST_X"
03 CLX
04 RCL ST Y
05 STO "ST_Y"
06 CLX
07 RCL ST Z
08 STO "ST_Z"
09 CLX
10 RCL ST T
11 STO "ST_T"
12 CLX
13 RCL ST L
14 STO "ST_L"
15 CLX
16 RCL "ST_X"

17 END

@ RCLST: Recall stack ----------------------------------------------------------
00 { 41-Byte Prgm }
01 LBL "RCLST"

02 RCL "ST_L"
03 STO STO ST L
04 RCL "ST_T"
05 RCL "ST_Z"
06 RCL "ST_Y"
07 RCL "ST_X"

08 END

@ CLTMP: Clear temporary variables ---------------------------------------------
00 { 72-Byte Prgm }
01 LBL "CLTMP"

02 SF 25 @ Ignore erros, if variables may not exist

03 CLV "D_CUST"
04 CLV "D_FMT"
05 CLV "D_DGT"
06 CLV "D_POL"

07 CLV "ST_L"
08 CLV "ST_T"
09 CLV "ST_Z"
10 CLV "ST_Y"
11 CLV "ST_X"

12 CF 25

13 END
