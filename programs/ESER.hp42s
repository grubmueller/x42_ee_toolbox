@ ESER: E-series for electrical components
@
@ Calculates E-series values. Input for E-series (3, 6, 12, 24,...) must
@ be entered into x stack before [EXEQ]. Result is stored as 1xE matrix named
@ ESER. To see result load ESER to x register by RCL. Then use the MARTRIX
@ EDIT function to browse through the result matrix.
@
@ For calculation of E series see Wikipedia article:
@ https://en.wikipedia.org/wiki/E_series_of_preferred_numbers
@
@ Michael Grubmüller, michael.grubmueller@gmail.com
@ 2023-05-10

00 { 150-Byte Prgm }
01▸LBL "ESER"
02 STO 00
03 1
04 DIM "ESER"
05 FIX 01
06 24
07 RCL 00
08 X>Y?
09 FIX 02
10 RCL 00
11 1
12 -
13 1000
14 ÷
15 0.00001
16 +
17 STO 01
18 INDEX "ESER"
19▸LBL 01
20 10
21 RCL 01
22 IP
23 RCL 00
24 ÷
25 Y↑X
26 RND
27 2.6
28 X<>Y
29 X=Y?
30 2.7
31 2.9
32 X<>Y
33 X=Y?
34 3.0
35 3.2
36 X<>Y
37 X=Y?
38 3.3
39 3.5
40 X<>Y
41 X=Y?
42 3.6
43 3.8
44 X<>Y
45 X=Y?
46 3.9
47 4.2
48 X<>Y
49 X=Y?
50 4.3
51 4.6
52 X<>Y
53 X=Y?
54 4.7
55 8.3
56 X<>Y
57 X=Y?
58 8.2
59 STOEL
60 I+
61 ISG 01
62 GTO 01
63 END
